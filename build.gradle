plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'org.moddingx'
java.toolchain.languageVersion = JavaLanguageVersion.of(25)

repositories {
    mavenCentral()
}

dependencies {
    api 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    api 'com.google.code.gson:gson:2.13.2'
    api 'net.neoforged:srgutils:1.0.10'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.14.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.14.2'
}

var sourcesJar = tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    dependsOn tasks.named('classes')
}

tasks.named('build').configure { task ->
    task.dependsOn sourcesJar
}

javadoc {
    options.addStringOption("link", "https://docs.oracle.com/en/java/javase/25/docs/api/")
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'moddingx'
            url 'https://maven.moddingx.org/release'
            credentials(PasswordCredentials)
        }
    }
}
